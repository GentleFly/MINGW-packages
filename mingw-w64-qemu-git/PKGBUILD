# Maintainer: Alexey Muhamatnabeev <muhamatnabeev@yandex.ru>

_realname=qemu-git
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=v3.1.0.rc2.r31.d522fba244
pkgrel=1
pkgdesc="A generic and open source machine emulator and virtualizer. Git version."
arch=('any')
url="https://qemu.org/"
license=('GPL2' 'LGPL2')
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-gcc"
    "${MINGW_PACKAGE_PREFIX}-python2"
    'perl'
    'bison'
)
depends=(
    "${MINGW_PACKAGE_PREFIX}-capstone"
     "${MINGW_PACKAGE_PREFIX}-curl"
     "${MINGW_PACKAGE_PREFIX}-glib2"
     "${MINGW_PACKAGE_PREFIX}-gnutls"
     "${MINGW_PACKAGE_PREFIX}-gtk3"
     "${MINGW_PACKAGE_PREFIX}-libjpeg"
     "${MINGW_PACKAGE_PREFIX}-libpng"
     "${MINGW_PACKAGE_PREFIX}-libssh2"
     "${MINGW_PACKAGE_PREFIX}-libusb"
     "${MINGW_PACKAGE_PREFIX}-lzo2"
     "${MINGW_PACKAGE_PREFIX}-pixman"
     "${MINGW_PACKAGE_PREFIX}-snappy"
     "${MINGW_PACKAGE_PREFIX}-SDL"
     "${MINGW_PACKAGE_PREFIX}-usbredir"
)
conflicts=(
    "${MINGW_PACKAGE_PREFIX}-${_realname%-git}"
)
source=(
    "git://git.qemu.org/qemu.git"
)
md5sums=(
    'SKIP'
)

pkgver() {
   cd "$srcdir/${_realname%-git}"
   printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
    cd "$srcdir/${_realname%-git}"
    git submodule update --init ui/keycodemapdb
    git submodule update --init capstone
    git submodule update --init dtc
}

build() {
    cd "$srcdir/${_realname%-git}"
    ./configure \
        --disable-stack-protector \
        --disable-werror \
        --enable-libssh2 \
        --enable-lzo \
        --enable-snappy \
        --enable-hax \
        --enable-gtk \
        --enable-sdl \
        --prefix=${MINGW_PREFIX} \
        --bindir=${MINGW_PREFIX}/bin \
        --datadir=${MINGW_PREFIX}/bin \
        --firmwarepath=${MINGW_PREFIX}/etc/qemu \
        --python=${MINGW_PREFIX}/bin/python2 \
        --mandir=${MINGW_PREFIX}/share/qemu \
        --docdir=${MINGW_PREFIX}/share/qemu
    make
}

#check() {
#    #cd "$srcdir/${pkgname%-git}"
#    #make -k check
#    echo "check()"
#}

package() {
    cd "$srcdir/${_realname%-git}"
    make DESTDIR="$pkgdir/" install
    echo "package()"
}
